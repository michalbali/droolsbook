package droolsbook.ruleflow.transform

import java.util.*;

import droolsbook.transform.service.LegacyBankService;
import droolsbook.bank.model.Address;
import droolsbook.bank.model.Address.Country;

global LegacyBankService legacyService;

rule findAllCustomers
ruleflow-group "default"
dialect "mvel"
	when
		$customerMap : Map( ) 
			from legacyService.findAllCustomers()		
	then	
		$customerMap["_type_"] = "Customer"	
		insert( $customerMap )
end


//stuff for reporting:
import droolsbook.bank.service.*;

global ValidationReport validationReport;
global ReportFactory reportFactory;

import function droolsbook.bank.service.ValidationHelper.*;



rule findAddress
ruleflow-group "default"
dialect "mvel"
	when
		$customerMap : Map( this["_type_"] == "Customer" )
		$addressMap : Map( ) 
			from legacyService.findAddressByCustomerId( 
				$customerMap["customer_id"] )		
	then		
		$addressMap["_type_"] = "Address"
		insert( $addressMap )
end

rule findAccount
ruleflow-group "default"
dialect "mvel"
	when
		$customerMap : Map( this["_type_"] == "Customer" )
		$accountMap : Map( ) from legacyService.findAccountByCustomerId( $customerMap["customer_id"])		
	then		
		$accountMap["_type_"] = "Account"
		insert( $accountMap )
end

rule twoEqualAddressesDifferentInstance
ruleflow-group "default"
	when
		$addressMap1 : Map( this["_type_"] == "Address" )
		$addressMap2 : Map( this["_type_"] == "Address", 
			eval( $addressMap1 != $addressMap2 ), 
			this == $addressMap1 )
	then
		retract( $addressMap2 );
		validationReport.addMessage(
			reportFactory.createMessage(Message.Type.WARNING, 
			kcontext.getRule().getName(), $addressMap2));
end

rule twoOrMoreAddresses
ruleflow-group "default"
salience -10 //should fire after the redundant address rule 
	when
		//$customerMap : Map( this["_type_"] == "Customer" )
		$addressMap1 : Map( this["_type_"] == "Address"//, 
		//	this["parent_id"] == $customerMap["customer_id"] 
		)
		$addressMap2 : Map( this["_type_"] == "Address", 
		//	this["parent_id"] == $customerMap["customer_id"],  
			this != $addressMap1 )
	then
		//error report that there are two addresses
		validationReport.addMessage(
			reportFactory.createMessage(Message.Type.ERROR, 
			kcontext.getRule().getName(), //$customerMap, 
			$addressMap1, $addressMap2));
end

// @extract-start 04 71
rule addressNormalizationUSA
ruleflow-group "address normalization"
// @extract-end
dialect "mvel"
	when
		$addressMap : Map( this["_type_"] == "Address", 
			this["country"] in ("US", "U.S.", "USA", "U.S.A"))
	then
		modify( $addressMap ) {
			put("country", Country.USA)
		}
end

rule addressNormalizationIreland
ruleflow-group "address normalization"
dialect "mvel" 
	when
		$addressMap : Map( this["_type_"] == "Address",
			this["country"] in ("Ireland", 
			"Republic of Ireland") )
	then
		modify( $addressMap ) {
			put("country", Country.Ireland)
		}
end

rule unknownCountry
ruleflow-group "unknown country"
	when
		$addressMap : Map( this["_type_"] == "Address",
		    eval(!($addressMap.get("country") instanceof 
		    Address.Country)))
	then
		validationReport.addMessage(
			reportFactory.createMessage(Message.Type.ERROR, 
			kcontext.getRule().getName(), $addressMap));
end

rule customerName
ruleflow-group "default"
	when
		//$customerMap : Map( name matches "^.+ .+$" ) //has space inside
	then
		//modify($customerMap) {
		//this["name_"] = $lCustomer.name.split[0];
		//} 
end


rule noCurrencyToEUR
ruleflow-group "default"
dialect "mvel"
	when
		$accountMap : Map( this["_type_"] == "Account", 
			this["currency"] == null )
	then
		modify($accountMap) {
			put("currency", "EUR")
		}
end

import java.math.BigDecimal;
function String getConversionToEurFrom(String currencyFrom) {
	String conversion = null;
	if ("USD".equals(currencyFrom)) {
		conversion = "0.670";
	} else if ("SKK".equals(currencyFrom)) {
		conversion = "0.033";
	}
	return conversion;
}

rule currencyConversionToEUR
ruleflow-group "default"
	when
		$accountMap : Map( this["_type_"] == "Account", 
			this["currency"] != null && != "EUR" )
		$conversionAmount : String() from 
			getConversionToEurFrom($accountMap["currency"])
	then
		modify($accountMap) {
			put("currency", "EUR"),	 
			put("balance", new BigDecimal(
				$conversionAmount).multiply(new BigDecimal(
				(String)$accountMap.get("balance"))))
		} 
end

rule unknownCurrency
ruleflow-group "default"
	when
		$accountMap : Map( this["_type_"] == "Account", 
			this["currency"] != null && != "EUR" )
		not( String() from 
			getConversionToEurFrom($accountMap["currency"]) )
	then
		 validationReport.addMessage(
			reportFactory.createMessage(Message.Type.ERROR, 
			kcontext.getRule().getName(), $accountMap));
end

/* doesn't work with mvel, it keeps casting it to Integer or Float
rule currencyConversionToEUR
ruleflow-group "default"
dialect "mvel"
	when
		$accountMap : Map( this["_type_"] == "Account", 
			this["currency"] != null && != "EUR" )
	then
		modify($accountMap) {
			this["currency"] = "EUR",	 
			this["balance"] = (BigDecimal) new BigDecimal($accountMap.get("balance")).multiply(getConversionToEurFrom($accountMap.get("balance")))
		} 
end
*/


/*
rule noLegacyAccount
ruleflow-group "default"
dialect "mvel"
	when
		$lCustomer : LegacyCustomer( )
		not LegacyAccount( customerId == $lCustomer.id )
	then
		//create default account
		LegacyAccount account = new LegacyAccount()		
		insert(account) 
end
*/

rule reduceLegacyAccounts
ruleflow-group "default"
	when
		$accountMap1 : Map( this["_type_"] == "Account" )
		$accountMap2 : Map( this["_type_"] == "Account", 
			eval( $accountMap1 != $accountMap2 ), 
			this["customer_id"] ==$accountMap1["customer_id"],
			this["currency"] == $accountMap1["currency"])
	then
		modify($accountMap1) {
			put("balance", (
				(BigDecimal)$accountMap1.get("balance")).add(
				(BigDecimal)$accountMap2.get("balance")))
		} 
		retract( $accountMap2 );
end

query getCustomer
	$customerMap : Map( this["_type_"] == "Customer" )
end

query getAccountByCustomerId (Map customerMap)
	$accountMap : Map( this["_type_"] == "Account", 
		this["customer_id"] == customerMap["customer_id"] )
end

query getAddressByCustomerId (Map customerMap)
	$addressMap : Map( this["_type_"] == "Address", 
		this["parent_id"] == customerMap["customer_id"] )
end